plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id 'biz.aQute.bnd.builder' version "$bndVersion"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

scmVersion {
    tag {
        prefix = 'whistlepost-i18n-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = ['main': 'simple']
    nextVersion {
        suffix = 'pre'
        separator = '-'
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = 8
targetCompatibility = 8

group = 'org.whistlepost'
version = rootProject.scmVersion.version

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

publishing {
    publications {
        "$name"(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().appendNode('name', name)
                asNode().appendNode('description', description)
                asNode().appendNode('url', 'https://www.whistlepost.org')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/whistlepost/whistlepost-i18n')
                scmNode.appendNode('connection', 'scm:git@github.com:whistlepost/whistlepost-i18n.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:whistlepost/whistlepost-i18n.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'Apache License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/whistlepost/whistlepost-i18n/master/LICENSE')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots/" :
                    "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications[name]
}
